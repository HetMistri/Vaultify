plugins {
    id 'java'
    id 'application'
}

group = 'com.vaultify'
version = '0.1-beta'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.postgresql:postgresql:42.7.4'
    implementation 'com.google.code.gson:gson:2.10.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
            exclude 'test/**'
        }
        resources {
            srcDirs = ['resources']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
    }
}

application {
    mainClass = 'com.vaultify.app.VaultifyApplication'
}

// Configure the default 'run' task to accept stdin
run {
    standardInput = System.in
}

jar {
    // Ensure duplicate classes/resources don't break packaging
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    // Jar manifest (for java -jar)
    manifest {
        attributes(
                'Main-Class': 'com.vaultify.app.VaultifyApplication'
        )
    }

    // Build fat jar (includes all runtime dependencies)
    from {
        configurations.runtimeClasspath.collect { file ->
            file.isDirectory() ? file : zipTree(file)
        }
    }
}



tasks.register('runLocal', JavaExec) {
    group = "vaultify"
    description = "Run Vaultify locally without Docker"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.vaultify.app.VaultifyApplication'
    standardInput = System.in
}

tasks.register('rebuild') {
    group = "vaultify"
    description = "Clean and build the entire project"
    dependsOn 'clean', 'build'
}

tasks.register('dockerBuild') {
    group = "vaultify"
    description = "Build Docker images after building jar"
    dependsOn 'build'
    doLast {
        exec {
            commandLine 'docker', 'compose', 'build'
        }
    }
}

test {
    useJUnitPlatform()
}
